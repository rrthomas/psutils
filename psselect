#!/usr/bin/env perl
my $version_banner = <<END;
FIXME: generate this banner
psselect 2.00
Copyright (c) Reuben Thomas 2016
Released under the GPL version 3, or (at your option) any later version.
END

use warnings;
use strict;

use File::Basename;
use Getopt::Long;

my $prog = basename($0);
my ($help_flag, $version_flag, $pages, $even_flag, $odd_flag, $reverse_flag, $quiet_flag);
$odd_flag = $even_flag = 0;

sub usage {
  my ($exit_code) = @_;
  print STDERR <<END;
$prog [OPTION...] [INFILE [OUTFILE]]
Select pages from a PostScript file.

  --pages=PAGES        select the given page ranges
                       FIXME: document syntax
  --even               select even-numbered pages
  --odd                select odd-numbered pages
  --reverse            reverse the order of the pages
  --quiet              don't show page numbers being output
  --help               display this help and exit
  --version            display version information and exit
END
  exit $exit_code;
}

# Get arguments
Getopt::Long::Configure("bundling");
# Having configured bundling, must give short options explicitly
my $opts = GetOptions(
  "pages|p=s" => \$pages,
  "even|e" => \$even_flag,
  "odd|o" => \$odd_flag,
  "reverse|r" => \$reverse_flag,
  "quiet|q" => \$quiet_flag,
  "help" => \$help_flag,
  "version" => \$version_flag,
 ) or usage(1);
if ($version_flag) {
  print STDERR $version_banner;
  exit 0;
}
usage(0) if $help_flag;

# If we haven't gotten a page range yet, we'd better get one now
if (!$pages) {
  usage(1) if $#ARGV == -1;
  if (!$reverse_flag && !$even_flag && !$odd_flag && $#ARGV > -1) {
    $pages = shift @ARGV;
  } else {
    $pages = "1-_1";
  }
}

# Rearrange the pages
my @arg = ();
push @arg, "-r" if $reverse_flag;
push @arg, "-e" if $even_flag;
push @arg, "-o" if $odd_flag;
exec("pstops", "-R$pages", @arg, "0", @ARGV) or die("error running pstops");
